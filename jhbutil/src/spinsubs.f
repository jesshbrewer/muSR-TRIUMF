C...SPINSUBS...
C
C...PACKAGE OF GENERAL-PURPOSE SUBROUTINES FOR USE IN
C   CONSTRUCTING SPIN HAMILTONIANS, ETC...
C
	SUBROUTINE PAUL44 (M, N, TWOSIG)
C
	COMPLEX TWOSIG(4,4)
C
C  4X4 PAULI-LIKE MATRICES FOR TWO DIRAC PARTICLES ---
C  BASIS IS IS THAT OF M OF PARTICLES A AND B --
C  (SA, SB) = SIGN OF M(A), SIGN OF M(B), SO (+,-) = A UP, B DOWN, ETC.
C  (1) = (++), (2) = (+-), (3) = (-+), (4) = (--)
C
	COMPLEX SIGA(2,2), SIGB(2,2), BLOCK(4,2,2)
	COMPLEX A11, A12, A21, A22
	EQUIVALENCE (SIGA(1,1),A11), (SIGA(1,2),A12)
	EQUIVALENCE (SIGA(2,1),A21), (SIGA(2,2),A22)
	COMPLEX S11(2,2), S12(2,2), S21(2,2), S22(2,2)
	EQUIVALENCE (S11(1,1), BLOCK(1,1,1)), (S12(1,1), BLOCK(1,1,2)),
     1 (S21(1,1), BLOCK(1,2,1)), (S22(1,1), BLOCK(1,2,2))
C
	IF ((M.LT.0).OR.(N.LT.0).OR.(M.GT.3).OR.(N.GT.3)) GO TO 1234
C
	CALL PAULI (M, SIGA)
	CALL PAULI (N, SIGB)
C
	CALL CSTMAT (0, 2, A11, SIGB, S11)
	CALL CSTMAT (0, 2, A12, SIGB, S12)
	CALL CSTMAT (0, 2, A21, SIGB, S21)
	CALL CSTMAT (0, 2, A22, SIGB, S22)
C
	CALL CBLKUD (1, 4, 2, BLOCK, TWOSIG)
C
	RETURN
C
 1234	WRITE (*,123) M, N
  123	FORMAT (1H1,10(/),20X,'------------- STUPID, YOU TRIED TO CALL ',
     1     'PAUL44 WITH M =',I3,'   AND N =',I3,'  -- GOODBYE.')
	STOP
C
	END
C
	SUBROUTINE PAULI (K, SIGMA)
C
	COMPLEX SIGMA(2,2)
	COMPLEX SIGMAS(2,2,4)
	REAL RIGMAS(4,2,4)
	EQUIVALENCE (SIGMAS,RIGMAS)
	DATA RIGMAS /0., 0.,   1., 0.,   1., 0.,   0., 0.,
     2  0., 0.,   0., 1.,   0.,-1.,   0., 0.,
     3  1., 0.,   0., 0.,   0., 0.,  -1., 0.,
     4  1., 0.,   0., 0.,   0., 0.,   1., 0.	 /
C  SIGMAS(4) = UNIT MATRIX.
C
	IF ((K.LT.0) .OR. (K.GT.4)) GO TO 1234
	KK = K
	IF (K .EQ. 0) KK = 4
C
	DO 100 I=1,2
	DO 100 J=1,2
  100	SIGMA(I,J) = SIGMAS(I,J,KK)
C
	RETURN
C
 1234	WRITE (*,123) K
  123	FORMAT (1H1,10(/),20X,'------------- STUPID, YOU TRIED TO CALL ',
     1'PAULI WITH K =',I3,'.  WHAT ARE YOU THINKING OF -- GOODBYE.')
	STOP
C
	END
